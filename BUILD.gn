# Copyright (c) 2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

config("ace_napi_config") {
  include_dirs = [
    "//foundation/ace/napi",
    "//foundation/ace/napi/interfaces/kits",
    "//third_party/libuv/include",
    "//third_party/node/src",
  ]
}

if (defined(ohos_lite)) {
  import("//build/lite/config/component/lite_component.gni")
  import("//build/lite/config/subsystem/aafwk/config.gni")

  config("ace_napi_jerryscript_config") {
    include_dirs = [
      "//foundation/ace/napi",
      "//foundation/ace/napi/native_engine",
      "//foundation/ace/napi/native_engine/impl/jerryscript",
      "//third_party/jerryscript/jerry-ext/include",
      "//third_party/jerryscript/jerry-core/include",
      "//third_party/jerryscript/jerry-port/default/include",
      "//third_party/libuv/include",
    ]
  }

  shared_library("ace_napi") {
    public_configs = [ ":ace_napi_config" ]

    include_dirs = [
      "//third_party/libuv/include",
      "//foundation/ace/napi",
      "//foundation/ace/napi/interfaces/kits",
      "//third_party/node/src",
      "//third_party/bounds_checking_function/include",
    ]

    sources = [
      "callback_scope_manager/native_callback_scope_manager.cpp",
      "module_manager/native_module_manager.cpp",
      "native_engine/native_api.cpp",
      "native_engine/native_async_work.cpp",
      "native_engine/native_engine.cpp",
      "native_engine/native_node_api.cpp",
      "native_engine/native_safe_async_work.cpp",
      "scope_manager/native_scope_manager.cpp",
    ]

    if (current_cpu == "arm64") {
      defines = [ "_ARM64_" ]
    }

    deps = [
      "//base//hiviewdfx/hilog_lite/frameworks/featured:hilog_shared",
      "//third_party/bounds_checking_function:libsec_shared",
      "//third_party/libuv:uv",
    ]

    cflags = [ "-fPIC" ]
  }

  shared_library("ace_napi_jerryscript") {
    configs -= [ "//build/lite/config:language_cpp" ]

    public_configs = [ ":ace_napi_jerryscript_config" ]

    include_dirs = [
      "//foundation/ace/napi",
      "//foundation/ace/napi/native_engine",
      "//foundation/ace/napi/native_engine/impl/jerryscript",
      "//third_party/jerryscript/jerry-ext/include",
      "//third_party/jerryscript/jerry-core/include",
      "//third_party/jerryscript/jerry-port/default/include",
      "//third_party/libuv/include",
      "//third_party/bounds_checking_function/include",
      "//base/hiviewdfx/hilog_lite/interfaces/native/innerkits",
    ]

    cflags = [ "-fPIC" ]
    cflags_cc = cflags
    cflags_cc += [ "-std=c++14" ]

    ldflags = [
      "-lstdc++",
      "-lpthread",
    ]

    sources = [
      "native_engine/impl/jerryscript/jerryscript_ext.cpp",
      "native_engine/impl/jerryscript/jerryscript_native_deferred.cpp",
      "native_engine/impl/jerryscript/jerryscript_native_engine.cpp",
      "native_engine/impl/jerryscript/jerryscript_native_reference.cpp",
      "native_engine/impl/jerryscript/native_value/jerryscript_native_array.cpp",
      "native_engine/impl/jerryscript/native_value/jerryscript_native_array_buffer.cpp",
      "native_engine/impl/jerryscript/native_value/jerryscript_native_big_int.cpp",
      "native_engine/impl/jerryscript/native_value/jerryscript_native_boolean.cpp",
      "native_engine/impl/jerryscript/native_value/jerryscript_native_buffer.cpp",
      "native_engine/impl/jerryscript/native_value/jerryscript_native_data_view.cpp",
      "native_engine/impl/jerryscript/native_value/jerryscript_native_date.cpp",
      "native_engine/impl/jerryscript/native_value/jerryscript_native_external.cpp",
      "native_engine/impl/jerryscript/native_value/jerryscript_native_function.cpp",
      "native_engine/impl/jerryscript/native_value/jerryscript_native_number.cpp",
      "native_engine/impl/jerryscript/native_value/jerryscript_native_object.cpp",
      "native_engine/impl/jerryscript/native_value/jerryscript_native_string.cpp",
      "native_engine/impl/jerryscript/native_value/jerryscript_native_typed_array.cpp",
      "native_engine/impl/jerryscript/native_value/jerryscript_native_value.cpp",
    ]

    deps = [
      ":ace_napi",
      "//base//hiviewdfx/hilog_lite/frameworks/featured:hilog_shared",
      "//third_party/bounds_checking_function:libsec_shared",
      "//third_party/jerryscript/jerry-core:jerry-core_shared",
      "//third_party/jerryscript/jerry-ext:jerry-ext_shared",
      "//third_party/jerryscript/jerry-port/default:jerry-port-default_shared",
    ]
  }
} else {
  import("//build/ohos.gni")
  import("//foundation/ace/ace_engine/ace_config.gni")

  ohos_shared_library("ace_napi") {
    defines = []
    public_configs = [ ":ace_napi_config" ]

    include_dirs = [
      "//foundation/ace/napi",
      "//foundation/ace/napi/interfaces/kits",
      "//third_party/libuv/include",
      "//third_party/node/src",
      "//utils/native/base/include",
    ]

    sources = [
      "callback_scope_manager/native_callback_scope_manager.cpp",
      "module_manager/native_module_manager.cpp",
      "native_engine/native_api.cpp",
      "native_engine/native_async_work.cpp",
      "native_engine/native_engine.cpp",
      "native_engine/native_node_api.cpp",
      "native_engine/native_safe_async_work.cpp",
      "reference_manager/native_reference_manager.cpp",
      "scope_manager/native_scope_manager.cpp",
    ]

    if (current_cpu == "arm64") {
      defines += [ "_ARM64_" ]
    }

    if (use_mingw_win) {
      defines += [ "WINDOWS_PLATFORM" ]
    } else if (use_mac) {
      defines += [ "MAC_PLATFORM" ]
    } else {
      external_deps = hilog_deps
    }

    deps = [
      "//third_party/libuv:uv_static",
      "//utils/native/base:utilsecurec",
    ]

    cflags_cc = [ "-Wno-missing-braces" ]

    subsystem_name = "ace"
    part_name = "napi"
  }

  template("ace_napi_lib") {
    forward_variables_from(invoker, "*")

    ohos_shared_library(target_name) {
      public_configs =
          [ "native_engine/impl/${engine_path}:ace_napi_${engine_path}_config" ]
      deps =
          [ "native_engine/impl/${engine_path}:ace_napi_impl_${engine_path}" ]
      subsystem_name = "ace"
      part_name = "napi"
    }
  }
  napi_libs = []

  foreach(item, ace_platforms) {
    if (item.name == "ohos") {
      engine_config = item.config
      support_engines = engine_config.js_engines
      foreach(engine, support_engines) {
        if (engine.engine_name != "ark") {
          ace_napi_lib("ace_napi_${engine.engine_path}") {
            engine_path = engine.engine_path
          }
          napi_libs += [ ":ace_napi_${engine.engine_path}" ]
        } else if (engine.engine_name == "ark") {
          ace_napi_lib("ace_napi_${engine.engine_name}") {
            engine_path = engine.engine_name
          }
          napi_libs += [ ":ace_napi_${engine.engine_name}" ]
        }
      }
    }
  }

  group("napi_packages") {
    deps = [ ":ace_napi" ]
    deps += napi_libs
  }

  ohos_ndk_library("libnapi_ndk") {
    ndk_description_file = "./libnapi.ndk.json"
    min_compact_version = "1"
    output_name = "ace_napi"
  }

  ohos_ndk_headers("napi_header") {
    dest_dir = "$ndk_headers_out_dir/napi"
    sources = [
      "//foundation/ace/napi/interfaces/kits/napi/native_api.h",
      "//foundation/ace/napi/interfaces/kits/napi/native_common.h",
      "//third_party/node/src/js_native_api.h",
      "//third_party/node/src/js_native_api_types.h",
      "//third_party/node/src/node_api.h",
      "//third_party/node/src/node_api_types.h",
    ]
  }

  ohos_shared_library("ace_napi_ndk") {
    public_configs = [ ":ace_napi_config" ]

    include_dirs = [
      "//foundation/ace/napi",
      "//foundation/ace/napi/interfaces/kits",
      "//third_party/libuv/include",
      "//third_party/node/src",
      "//utils/native/base/include",
    ]
    sources = [
      "callback_scope_manager/native_callback_scope_manager.cpp",
      "module_manager/native_module_manager.cpp",
      "native_engine/native_api.cpp",
      "native_engine/native_async_work.cpp",
      "native_engine/native_engine.cpp",
      "native_engine/native_node_api.cpp",
      "native_engine/native_safe_async_work.cpp",
      "scope_manager/native_scope_manager.cpp",
    ]

    if (current_cpu == "arm64") {
      defines = [ "_ARM64_" ]
    }

    deps = [
      ":libnapi_ndk",
      "//third_party/libuv:uv_static",
      "//utils/native/base:utilsecurec",
    ]

    cflags_cc = [ "-Wno-missing-braces" ]

    if (is_standard_system) {
      external_deps = [ "hiviewdfx_hilog_native:libhilog" ]
    } else {
      external_deps = [ "hilog:libhilog" ]
    }

    version_script =
        get_label_info(":libnapi_ndk", "target_gen_dir") + "/" +
        get_label_info(":libnapi_ndk", "name") + version_script_suffix
    subsystem_name = "ace"
    part_name = "napi"
  }

  group("napi_packages_ndk") {
    deps = [ ":ace_napi_ndk" ]
  }

  if (!build_ohos_ndk) {
    group("napi_packages_test") {
      testonly = true

      deps = [
        "sample/native_module_demo:demo",
        "sample/native_module_netserver:netserver",
        "sample/native_module_storage:storage",
        "test/unittest:unittest",
      ]

      if (is_standard_system) {
        deps += [ "sample/native_module_ability:ability" ]
      }
    }
  }
}
